plugins {
    id 'java'
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id "com.diffplug.gradle.spotless"
    id "com.jfrog.bintray"
}

ext {
    // バージョン定義
    versionMajor = 0
    versionMinor = 0
    versionPatch = 5

    // アーティファクト関連
    group 'com.epion_t3'
    version "${versionMajor}.${versionMinor}.${versionPatch}"

    // Document
    siteUrl = 'https://docs.epion-t3.com'

    // SCM（GitHub）
    githubUrl = 'https://github.com/epion-tropic-test-tool/epion-t3-core'
}

repositories {
    maven {
        url "https://dl.bintray.com/epion-tropic-test-tool/maven"
    }
    mavenCentral()
    jcenter()
}

dependencies {

    // Json & Yaml
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.6'
    api group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.6'
    api group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.6'
    api group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.9.6'

    // Java Utils
    api group: 'com.google.guava', name: 'guava', version: '25.1-jre'

    // logging
    api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    api group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    // cli interface
    api group: 'commons-cli', name: 'commons-cli', version: '1.4'

    // lang
    api group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'

    // io
    api group: 'commons-io', name: 'commons-io', version: '2.6'

    // Bean Utils
    api group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.3'

    // Text
    api group: 'org.apache.commons', name: 'commons-text', version: '1.4'

    // BeanValidation
    api group: 'javax.validation', name: 'validation-api', version: '1.1.0.Final'
    api group: 'org.apache.bval', name: 'bval-jsr', version: '1.1.2'

    // Reporting
    api group: 'org.thymeleaf', name: 'thymeleaf', version: '3.0.9.RELEASE'
    api group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-java8time', version: '3.0.1.RELEASE'

    // JSON Path
    api group: 'com.jayway.jsonpath', name: 'json-path', version: '2.4.0'

    // Activity
    api group: 'net.sourceforge.plantuml', name: 'plantuml', version: '8059'

    // XML
    api group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    api group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '2.3.0'
    api group: 'javax.activation', name: 'javax.activation-api', version: '1.2.0'

    // For Develop
    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'

    testCompile 'junit:junit:4.12'

}

// JavaDoc
tasks.withType(Javadoc) {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
    options.addBooleanOption('Xdoclint:none', true)
}

// Java
tasks.withType(JavaCompile) {
    sourceCompatibility = 11
    targetCompatibility = 11
    it.options.encoding = 'UTF-8'
}

// JavaDoc Jar packaging
task packageJavadoc(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// Source Jar Packaging
task packageSources(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'source'
}

artifacts {
    archives packageJavadoc
    archives packageSources
}

// CodeCov
task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination project.file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

spotless {
    java {
        enforceCheck = true
        licenseHeaderFile rootProject.file('./formatter/LICENSE')
        eclipse().configFile rootProject.file('./formatter/formatter.xml')
        lineEndings 'UNIX'
    }
}

// Library Publish
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publish = true
    override = true
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'epion-tropic-test-tool'
        licenses = ['Apache2']
        websiteUrl = siteUrl
        issueTrackerUrl = githubUrl + '/blob/master/LICENSE'
        vcsUrl = githubUrl + '.git'
        issueTrackerUrl = githubUrl + '/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name project.name
            url siteUrl

            packaging 'jar'
            groupId project.group
            artifactId project.name
            version project.version

            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'http://www.apache.org/licenses/'
                    distribution 'repo'
                }
            }
            scm {
                connection githubUrl + '.git'
                url githubUrl
            }
        }
    }
}
